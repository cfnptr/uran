// Copyright 2020-2022 Nikita Fediuchin. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

// Authors: Ian McEwan, Ashima Arts.
// Distributed under the MIT License.

#ifndef NOISE_H
#define NOISE_H

float mod289(float x)
{
	const float d = 1.0 / 289.0;
	return x - floor(x * d) * 289.0;
}
vec2 mod289(vec2 x)
{
	const float d = 1.0 / 289.0;
	return x - floor(x * d) * 289.0;
}
vec3 mod289(vec3 x)
{
	const float d = 1.0 / 289.0;
	return x - floor(x * d) * 289.0;
}
vec4 mod289(vec4 x)
{
	const float d = 1.0 / 289.0;
	return x - floor(x * d) * 289.0;
}

float permute(float x)
{
	return mod289(((x * 34.0) + 10.0) * x);
}
vec3 permute(vec3 x)
{
	return mod289(((x * 34.0) + 10.0) * x);
}
vec4 permute(vec4 x)
{
	return mod289(((x * 34.0) + 10.0) * x);
}

float taylorInvSqrt(float r)
{
	return 1.79284291400159 - 0.85373472095314 * r;
}
vec4 taylorInvSqrt(vec4 r)
{
	return 1.79284291400159 - 0.85373472095314 * r;
}

vec4 grad4(float j, vec4 ip)
{
	const vec4 ones = vec4(
		1.0, 1.0, 1.0, -1.0);
	vec4 p, s;

	p.xyz = floor(fract(vec3(j) * ip.xyz) * 7.0) * ip.z - 1.0;
	p.w = 1.5 - dot(abs(p.xyz), ones.xyz);
	s = vec4(lessThan(p, vec4(0.0)));
	p.xyz = p.xyz + (s.xyz * 2.0 - 1.0) * s.www;
	return p;
}

float calcSimplex2D(vec2 v)
{
	const vec4 C = vec4(
		0.211324865405187,
		0.366025403784439,
		-0.577350269189626,
		0.024390243902439);
	
	vec2 i = floor(v + dot(v, C.yy));
	vec2 x0 = v - i + dot(i, C.xx);

	vec2 i1 = (x0.x > x0.y) ?
		vec2(1.0, 0.0) : vec2(0.0, 1.0);
	
	vec4 x12 = x0.xyxy + C.xxzz;
	x12.xy -= i1;
	i = mod289(i);
	
	vec3 p = permute(permute(i.y +
		vec3(0.0, i1.y, 1.0)) + i.x +
		vec3(0.0, i1.x, 1.0));

	vec3 m = max(0.5 - vec3(
		dot(x0, x0),
		dot(x12.xy, x12.xy),
		dot(x12.zw, x12.zw)), 0.0);
	
	m = m * m;
	m = m * m;

	vec3 x = 2.0 * fract(p * C.www) - 1.0;
	vec3 h = abs(x) - 0.5;
	vec3 ox = floor(x + 0.5);
	vec3 a0 = x - ox;

	m *= 1.79284291400159 - 0.85373472095314 * (a0 * a0 + h * h);

	vec3 g;
	g.x  = a0.x * x0.x + h.x * x0.y;
	g.yz = a0.yz * x12.xz + h.yz * x12.yw;
	return 130.0 * dot(m, g);
}
float calcSimplex3D(vec3 v)
{
	const vec2 C = vec2(1.0 / 6.0, 1.0 / 3.0);
	const vec4 D = vec4(0.0, 0.5, 1.0, 2.0);

	vec3 i = floor(v + dot(v, C.yyy));
	vec3 x0 = v - i + dot(i, C.xxx);
	vec3 g = step(x0.yzx, x0.xyz);
	vec3 l = 1.0 - g;
	vec3 i1 = min(g.xyz, l.zxy);
	vec3 i2 = max(g.xyz, l.zxy);
	vec3 x1 = x0 - i1 + C.xxx;
	vec3 x2 = x0 - i2 + C.yyy;
	vec3 x3 = x0 - D.yyy;

	i = mod289(i);
	
	vec4 p = permute(permute(permute(
		i.z + vec4(0.0, i1.z, i2.z, 1.0)) +
		i.y + vec4(0.0, i1.y, i2.y, 1.0)) +
		i.x + vec4(0.0, i1.x, i2.x, 1.0));

	const float n_ = 1.0 / 7.0;
	vec3  ns = n_ * D.wyz - D.xzx;
	vec4 j = p - 49.0 * floor(p * ns.z * ns.z);
	vec4 x_ = floor(j * ns.z);
	vec4 y_ = floor(j - 7.0 * x_);
	vec4 x = x_ * ns.x + ns.yyyy;
	vec4 y = y_ * ns.x + ns.yyyy;
	vec4 h = 1.0 - abs(x) - abs(y);
	vec4 b0 = vec4(x.xy, y.xy);
	vec4 b1 = vec4(x.zw, y.zw);
	vec4 s0 = floor(b0) * 2.0 + 1.0;
	vec4 s1 = floor(b1) * 2.0 + 1.0;
	vec4 sh = -step(h, vec4(0.0));
	vec4 a0 = s0.xzyw * sh.xxyy + b0.xzyw;
	vec4 a1 = s1.xzyw * sh.zzww + b1.xzyw;
	vec3 p0 = vec3(a0.xy, h.x);
	vec3 p1 = vec3(a0.zw, h.y);
	vec3 p2 = vec3(a1.xy, h.z);
	vec3 p3 = vec3(a1.zw, h.w);

	vec4 norm = taylorInvSqrt(vec4(
		dot(p0, p0), dot(p1, p1),
		dot(p2, p2), dot(p3, p3)));
	
	p0 *= norm.x;
	p1 *= norm.y;
	p2 *= norm.z;
	p3 *= norm.w;

	vec4 m = max(0.5 - vec4(
		dot(x0, x0), dot(x1, x1),
		dot(x2, x2), dot(x3, x3)), 0.0);
	
	m = m * m;
	
	return 105.0 * dot(m * m, vec4(
		dot(p0, x0), dot(p1, x1),
		dot(p2, x2), dot(p3, x3)));
}
float calcSimplex4D(vec4 v)
{
	const vec4 C = vec4(
		0.138196601125011,
		0.276393202250021,
		0.414589803375032,
		-0.447213595499958);

	const float F4 = 0.309016994374947451;
	vec4 i = floor(v + dot(v, vec4(F4)));
	vec4 x0 = v - i + dot(i, C.xxxx);
	vec4 i0;
	vec3 isX = step(x0.yzw, x0.xxx);
	vec3 isYZ = step(x0.zww, x0.yyz);
	i0.x = isX.x + isX.y + isX.z;
	i0.yzw = 1.0 - isX;
	i0.y += isYZ.x + isYZ.y;
	i0.zw += 1.0 - isYZ.xy;
	i0.z += isYZ.z;
	i0.w += 1.0 - isYZ.z;

	vec4 i3 = clamp(i0, 0.0, 1.0);
	vec4 i2 = clamp(i0 - 1.0, 0.0, 1.0);
	vec4 i1 = clamp(i0 - 2.0, 0.0, 1.0);

	vec4 x1 = x0 - i1 + C.xxxx;
	vec4 x2 = x0 - i2 + C.yyyy;
	vec4 x3 = x0 - i3 + C.zzzz;
	vec4 x4 = x0 + C.wwww;
	i = mod289(i);
	
	float j0 = permute(permute(permute(
		permute(i.w) + i.z) + i.y) + i.x);
	vec4 j1 = permute(permute(permute(permute(
		i.w + vec4(i1.w, i2.w, i3.w, 1.0))
		+ i.z + vec4(i1.z, i2.z, i3.z, 1.0))
		+ i.y + vec4(i1.y, i2.y, i3.y, 1.0))
		+ i.x + vec4(i1.x, i2.x, i3.x, 1.0));
	const vec4 ip = vec4(
		1.0 / 294.0, 1.0 / 49.0, 1.0 / 7.0, 0.0);

	vec4 p0 = grad4(j0, ip);
	vec4 p1 = grad4(j1.x, ip);
	vec4 p2 = grad4(j1.y, ip);
	vec4 p3 = grad4(j1.z, ip);
	vec4 p4 = grad4(j1.w, ip);

	vec4 norm = taylorInvSqrt(vec4(
		dot(p0, p0), dot(p1, p1), dot(p2, p2), dot(p3, p3)));
	
	p0 *= norm.x;
	p1 *= norm.y;
	p2 *= norm.z;
	p3 *= norm.w;
	p4 *= taylorInvSqrt(dot(p4, p4));

	vec3 m0 = max(0.6 - vec3(
		dot(x0, x0), dot(x1, x1), dot(x2, x2)), 0.0);
	vec2 m1 = max(0.6 - vec2(
		dot(x3, x3), dot(x4, x4)), 0.0);
	
	m0 = m0 * m0;
	m1 = m1 * m1;
	
	return 49.0 * (dot(m0 * m0, vec3(
		dot(p0, x0), dot(p1, x1), dot(p2, x2)))
		+ dot(m1 * m1, vec2(dot(p3, x3), dot(p4, x4))));
}

#endif
